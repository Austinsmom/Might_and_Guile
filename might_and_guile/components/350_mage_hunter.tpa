
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						WARRIOR KITS
//__________________________________________________________________________________
//__________________________________________________________________________________


//ADD MAGE HUNTER____________________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5MAGEH~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5MAGEH       2 2 2 2 2 2 2 2~
~D5MAGEH       2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   9   9   9   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH  	       0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5MAGEH           1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5MAGEH           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/ranger/d5_mageh.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_R_E K_R_H K_R_HE~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra3~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3503
SAY @3504
SAY @3505
//___________________________________________________________________________________

END

ACTION_IF FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN

ADD_KIT ~D5MAGEH~
//___________________________________________________________________________________

//WEAPON PROFICIENCIES________________________________________________________________
//11                                   T   S           F   Q             S S
//10           L S                     W   C           L   U             W I
//9            A M             B L S   O   I   W       A   A             O N
//8            R A             A O H   H   M   A       I   R C   S       R G
//7            G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6            E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5            S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4            W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3            O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2            R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1            D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5MAGEH       3 3 3 3 3 3 3 3~
~D5MAGEH       3 3 3 3 3 3 3 3 0 3 3 3 3 0 3 3 3 3 3 0 3 3 3 3 0 3 0 3 2 2 2 3 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   9   9   9   0   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH          -1   0  -1   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH           0   15  15  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5MAGEH  	       0   15  15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5MAGEH           1  0  0  1  0  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5MAGEH           0  0  0  0  0  0~

//KIT ABILITIES 2DA FILE______________________________________________________________
~might_and_guile/ranger/d5_mageh.2da~

//RACIAL KIT AVAILABILITY_____________________________________________________________
~K_R_E K_R_H K_R_HE~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra3~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG25 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @3503
SAY @3504
SAY @3505
//___________________________________________________________________________________

END

//EE KIT EXTRAS_____________________________________________________________________
// 
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@3506)
		STR_VAR
			kit_name = D5MAGEH
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________

//MAGE HUNTER ABILITY FILES__________________________________________________________
//
COPY ~might_and_guile/ranger/d5_mageh.2da~ ~override~
COPY ~might_and_guile/ranger/d5_rnosp.eff~ ~override~
COPY ~might_and_guile/ranger/d5_rnosp.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rasp1.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rasp2.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rasp3.spl~ ~override~
COPY ~might_and_guile/ranger/d5_stsp1.spl~ ~override~
COPY ~might_and_guile/ranger/d5_stsp2.spl~ ~override~
COPY ~might_and_guile/ranger/d5_stsp3.spl~ ~override~
COPY ~might_and_guile/ranger/d5_rmhsc.spl~ ~override~
COPY_EXISTING ~spwi115.spl~ ~override/d5_rasho.spl~
	WRITE_SHORT 0x1c 2
COPY_EXISTING ~spwi223.spl~ ~override/d5_radef.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 1
COPY_EXISTING ~spwi302.spl~ ~override/d5_rarem.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 2
COPY_EXISTING ~spwi310.spl~ ~override/d5_ranon.spl~
	WRITE_SHORT 0x1c 2
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_wiz.d5~ BEGIN
	COPY_EXISTING ~spwi318.spl~ ~override/d5_radfl.spl~
		WRITE_SHORT 0x1c 2
		WRITE_LONG 0x34 2
	COPY_EXISTING ~spwi321.spl~ ~override/d5_rathr.spl~
		WRITE_SHORT 0x1c 2
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_wiz.d5~ BEGIN
	COPY_EXISTING ~spwi318.spl~ ~override/d5_radfl.spl~
	COPY_EXISTING ~spwi321.spl~ ~override/d5_rathr.spl~
END
COPY_EXISTING ~spwi513.spl~ ~override/d5_rabre.spl~
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 3
/*
COPY_EXISTING ~spwi513.spl~ ~override/d5_rabre.spl~
	LPM spell_to_innate
*/
COPY ~might_and_guile/misc/d5_savbo.spl~ ~override~
COPY ~might_and_guile/ranger/d5_mghs1.spl~ ~override~
COPY ~might_and_guile/ranger/d5_mghsa.eff~ ~override~
COPY ~might_and_guile/ranger/d5_mghsb.eff~ ~override~
COPY ~might_and_guile/ranger/d5_mghsc.eff~ ~override~
COPY ~might_and_guile/ranger/d5_mghs2.spl~ ~override~
	SAY NAME1 @3508
	SAY UNIDENTIFIED_DESC @3509
COPY ~might_and_guile/ranger/d5_mghs1.eff~ ~override~
COPY ~might_and_guile/ranger/d5_mghs2.eff~ ~override~
//__________________________________________________________________________________

//2DA MODIFICATIONS___________________________________________________________________
ACTION_IF FILE_EXISTS_IN_GAME ~d5_strid.d5~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/strirang.txt~
		BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_stats.d5~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/misc/charisma_bonuses.txt~
		APPEND_FILE ~might_and_guile/misc/crit_changes.txt~
	BUT_ONLY
END
ACTION_IF GAME_IS ~iwdee~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/tracking.txt~
		BUT_ONLY
END
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_profs.d5~ THEN BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~d5_apr.d5~) OR (FILE_EXISTS_IN_GAME ~TB#TATT.spl~) THEN BEGIN
		COPY_EXISTING ~d5_mageh.2da~ ~override~
			LPM remove_blank_lines
			APPEND_FILE ~might_and_guile/profs/w1apr.txt~
			BUT_ONLY
	END
END
ACTION_IF FILE_EXISTS_IN_GAME ~d5_range.d5~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/ranstab.txt~
	BUT_ONLY
END
//____________________________________________________________________________________

//VANILLA SPELL TABLES______________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_staspl.d5~ THEN BEGIN
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/mhspl1.txt~
		BUT_ONLY
	COPY_EXISTING ~d5_mageh.2da~ ~override~
		LPM remove_blank_lines
		APPEND_FILE ~might_and_guile/ranger/raspl1.txt~
		BUT_ONLY
END
//____________________________________________________________________________________

//REVISED SPELL TABLES_______________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_taspl.d5~ THEN BEGIN
	COPY ~might_and_guile/ranger/d5_maghb.2da~ ~override/d5_mageh.2da~
END
//____________________________________________________________________________________

//______________________HLAS________________________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~TG#TOUG.spl~ THEN BEGIN
	COPY_EXISTING ~LURa0.2da~ ~override/LURa3.2da~	
END
//__________________________________________________________________________________


//FnP SPHERE SYSTEM COMPATIBILITY____________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5_spheres.d5~) BEGIN

ACTION_IF GAME_IS ~iwdee~ BEGIN
	OUTER_SPRINT game ~iwd~
END
ELSE BEGIN
	OUTER_SPRINT game ~bg2~
END

COPY_EXISTING ~d5_mageh.2da~ ~override~
	LPM remove_blank_lines
	APPEND_FILE ~faiths_and_powers/spheres/%game%/life_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/protection_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/vigor_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/plant_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/air_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/light_ranger.txt~
	APPEND_FILE ~faiths_and_powers/spheres/%game%/magic_ranger.txt~
BUT_ONLY

//__________________________________________________________________________________

OUTER_SPRINT kit_spheres @3510 // ........................KIT SPHERES TRA REF HERE

OUTER_SPRINT this_kit ~D5MAGEH~ // ..............................KIT NAME HERE

//__________________________________________________________________________________

OUTER_SET strref = (0 - 1)

COPY_EXISTING - ~kitlist.2da~ ~override~
	COUNT_2DA_COLS num_cols
	READ_2DA_ENTRIES_NOW ~r2en_kitlist~ num_cols
	FOR (row = 1; row < r2en_kitlist; row += 1) BEGIN
		READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 1 kitname
		PATCH_IF (~%kitname%~ STRING_EQUAL_CASE ~%this_kit%~) BEGIN
			READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 4 strref
		END
	END
BUT_ONLY

ACTION_IF (strref >= 0) BEGIN
	ACTION_GET_STRREF %strref% kit_description 
	OUTER_PATCH_SAVE kit_description ~%kit_description%~ BEGIN
		REPLACE_TEXTUALLY ~Abilities:~ ~%kit_spheres%~
	END
	STRING_SET_EVALUATE %strref% ~%kit_description%~
END

END
//__________________________________________________________________________________
